# Static (musl) builds of the STT binaries, published as release assets.
# Build: nix build --impure -f static.nix whisper-cli  (or: ffmpeg)
#
# Every override below disables an optional feature whose dependency either
# refuses to build statically (badPlatforms isStatic), fails to compile/link
# under musl, or drags a C++ archive into ffmpeg's C link. None are needed:
# whisper-cli only receives pre-converted 16kHz mono WAV from the CLI, and
# the downloaded ffmpeg only does local file-to-file audio conversion
# (no capture devices, network protocols, video encoders, or filters).
let
  ps = (builtins.getFlake "github:NixOS/nixpkgs/nixos-unstable")
    .legacyPackages.${builtins.currentSystem}.pkgsStatic;
in
{
  whisper-cli = ps.whisper-cpp.override {
    withSDL = false; # SDL2 -> libpulseaudio, which refuses static
    withFFmpegSupport = false;
    # only used to wrap the model-download script (not shipped); its test
    # suite fails under musl
    wget = ps.wget.overrideAttrs (_: { doCheck = false; });
  };

  ffmpeg = ps.ffmpeg-headless.override {
    withOpenmpt = false; # -> mpg123 -> libpulseaudio (refuses static)
    withV4l2 = false; # -> libbpf -> elfutils (refuses static)
    withVaapi = false; # libva refuses static
    withOpencl = false; # ocl-icd fails to link statically
    withCudaLLVM = false; # tries to build all of LLVM statically, fails
    withBluray = false; # -> fontconfig/python-fonttools build failures
    withFontconfig = false;
    withFreetype = false;
    withHarfbuzz = false;
    withOpenapv = false; # static pkg-config file broken
    withOpenjpeg = false; # -> libtiff -> libwebp -> giflib, fails on x86_64 musl
    withVulkan = false;
    withSoxr = false; # not found by configure; swresample suffices
    withSsh = false;
    withSrt = false;
    withRist = false;
    withGnutls = false; # -> nettle, GOT relocation overflow on aarch64
    withAom = false; # links libvmaf (C++) -> undefined stdc++ refs
    withSvtav1 = false;
    withTheora = false;
    withVpx = false;
    withWebp = false;
    withX264 = false;
    withX265 = false;
    withXvid = false;
    withVidStab = false;
    withZimg = false;
    withZvbi = false;
  };
}
